import org.gradle.internal.os.OperatingSystem
OperatingSystem os = OperatingSystem.current()

sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.main.resources.srcDirs = ["../core/assets"]

project.ext.mainClassName = "com.csse3200.game.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../core/assets")

task run(dependsOn: classes, type: JavaExec) {
    mainClass.set(project.mainClassName)
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    jvmArgs = os.isMacOsX() ?
        ["-Djava.util.logging.config.file=../../logging.properties", "-XstartOnFirstThread"] :
        ["-Djava.util.logging.config.file=../../logging.properties"]
}

task debug(dependsOn: classes, type: JavaExec) {
    mainClass.set(project.mainClassName)
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
    debugOptions {
        enabled = true
        port = 5005     // Port 5005 is the default port on which the IntelliJ debugger listens.
        server = true
        suspend = false
    }
}

task dist(type: Jar) {
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dist.dependsOn classes

eclipse.project.name = appName + "-desktop"

dependencies {
    implementation project(":core")
    testImplementation 'net.bytebuddy:byte-buddy:1.11.13'
    api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    api "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
}
